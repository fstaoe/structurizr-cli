import org.apache.tools.ant.taskdefs.condition.Os

plugins {
	id 'java'
	id 'org.graalvm.buildtools.native' version '0.10.2'
}

defaultTasks 'clean', 'build', 'getDeps', 'buildZip'

description = 'Structurizr CLI'
group = 'com.structurizr'
version = ''

sourceCompatibility = 17
targetCompatibility = 17

repositories {
	mavenCentral()
//	mavenLocal()
}

dependencies {

	implementation 'com.structurizr:structurizr-dsl:2.1.1'
	implementation 'com.structurizr:structurizr-export:2.1.1'
	implementation 'com.structurizr:structurizr-autolayout:2.1.1'
	implementation 'com.structurizr:structurizr-inspection:2.1.1'
	implementation 'io.github.goto1134:structurizr-d2-exporter:1.5.3'

	implementation 'commons-cli:commons-cli:1.5.0'

	implementation 'org.codehaus.groovy:groovy-jsr223:3.0.19'
	implementation 'org.jetbrains.kotlin:kotlin-scripting-jsr223:1.8.10'
	implementation 'org.jruby:jruby-core:9.4.4.0'

	implementation 'commons-logging:commons-logging:1.2'
	implementation 'org.slf4j:jcl-over-slf4j:2.0.13'
	implementation 'ch.qos.logback:logback-classic:1.5.6'

	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.9.2'

}

test {
	useJUnitPlatform()

	testLogging {
		events "failed" //, "passed", "skipped", "standardOut", "standardError"

		showExceptions true
		exceptionFormat "full"
		showCauses true
		showStackTraces true

		showStandardStreams = false
	}
}

sourceSets.main.resources {
	srcDirs = ['src/main/resources']
	include 'build.properties'
	include 'META-INF/**'
}

jar {
    manifest {
        attributes(
                'Implementation-Title': description,
                'Implementation-Version': archiveVersion
        )
    }
}

task getDeps(type: Copy) {
	from configurations.default
	into 'build/dependencies'
}

task buildZip(type: Zip) {
	from ('build/libs/') {
		include 'structurizr-cli*.jar'
		into 'lib'
	}
	from ('build/dependencies') {
		include '*.jar'
		into 'lib'
	}
	from ('.') {
		include 'README.md'
		include 'docs/*'
		include 'docs/images/*'
	}
	from ('etc') {
		include 'structurizr.sh'
		include 'structurizr.bat'
	}
}

graalvmNative {
	def classpathSeparator = Os.isFamily(Os.FAMILY_WINDOWS) ? ";" : ":"
	// Collect all JAR files from build/dependencies and build/libs
	def dependenciesJars = fileTree(dir: 'build/dependencies', include: '*.jar').files
	def libsJars = fileTree(dir: 'build/libs', include: '*.jar').files
	def classpath = (dependenciesJars + libsJars).collect { it.absolutePath }.join(classpathSeparator)

	testSupport = false
	metadataRepository {
		enabled = false
	}

	binaries {
		main {
			mainClass = "com.structurizr.cli.StructurizrCliApplication"
			buildArgs.addAll("--verbose", "-J-Xmx7g", "-cp", classpath)
		}
	}
}